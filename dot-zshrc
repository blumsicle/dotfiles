# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
    source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

[[ -f "/opt/homebrew/bin/brew" ]] && eval "$(/opt/homebrew/bin/brew shellenv)"

ZINIT_HOME="${XDG_DATA_HOME:-$HOME/.local/share}/zinit/zinit.git"
[[ ! -d $ZINIT_HOME ]] && mkdir -p "$(dirname $ZINIT_HOME)"
[[ ! -d $ZINIT_HOME/.git ]] && git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"

source "$ZINIT_HOME/zinit.zsh"

function zvm_config() {
    ZVM_LINE_INIT_MODE=$ZVM_MODE_INSERT
    ZVM_VI_INSERT_ESCAPE_BINDKEY=jk
}

zinit depth"1" light-mode for                                   \
    Aloxaf/fzf-tab                                              \
    zsh-users/zsh-syntax-highlighting                           \
    zsh-users/zsh-completions                                   \
    zsh-users/zsh-autosuggestions                               \
    jeffreytse/zsh-vi-mode                                      \
    pick'themes/catppuccin_frappe-zsh-syntax-highlighting.zsh'  \
        catppuccin/zsh-syntax-highlighting                      \
    atload'eval "$(starship init zsh)"'                         \
        starship/starship

zinit depth"1" light-mode nocompile cloneonly for   \
    atclone'mkdir -p $(bat --config-dir)/themes'    \
    atclone'cp themes/* $(bat --config-dir)/themes' \
    atclone'bat cache --build'                      \
    atpull'%atclone'                                \
    atload'export BAT_THEME="Catppuccin Frappe"'    \
        catppuccin/bat

export K9S_CONFIG_DIR="$HOME/.config/k9s"
zinit depth"1" light-mode nocompile cloneonly for   \
    atclone'mkdir -p $K9S_CONFIG_DIR/skins'         \
    atclone'cp dist/* $K9S_CONFIG_DIR/skins'        \
    atpull'%atclone'                                \
        catppuccin/k9s

export TMUX_PLUGIN_MANAGER_PATH="$HOME/.local/share/tmux/plugins"
zinit depth"1" light-mode nocompile cloneonly for   \
    atclone'mkdir -p $TMUX_PLUGIN_MANAGER_PATH/tpm' \
    atclone'rm -rf $TMUX_PLUGIN_MANAGER_PATH/tpm'   \
    atclone'cp -R . $TMUX_PLUGIN_MANAGER_PATH/tpm'  \
    atpull'%atclone'                                \
        tmux-plugins/tpm

export ZOXIDE_CMD_OVERRIDE=zz

zinit wait lucid for                \
    OMZP::brew                      \
    OMZP::eza                       \
    OMZP::gitignore                 \
    OMZP::python                    \
    OMZP::zoxide                    \
    OMZP::fzf                       \
    OMZP::git

typeset -U path PATH
typeset -U fpath FPATH

path=("$HOME/.local/bin" "$(go env GOPATH)/bin" "$HOME/.cargo/bin" $path)

[[ ! -d $HOME/.zfunc ]] && mkdir -p "$HOME/.zfunc"

fpath+="$HOME/.zfunc"

if [[ -f "/opt/homebrew/bin/brew" ]]; then
    fpath+="$(/opt/homebrew/bin/brew --prefix)/share/zsh/site-functions"
fi

autoload -Uz compinit && compinit

zinit cdreplay -q

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

HISTSIZE=5000
HISTFILE="$HOME/.zsh_history"
SAVEHIST=$HISTSIZE
HISTDUP=erase
setopt appendhistory
setopt sharehistory
setopt hist_ignore_space
setopt hist_ignore_all_dups
setopt hist_save_no_dups
setopt hist_ignore_dups
setopt hist_find_no_dups
setopt interactive_comments

zstyle ':completion:*' completer _complete _ignored _expand_alias
zstyle ':completion:*' regular false
zstyle ':completion:*' add-space false
zstyle ':completion:*:git-checkout:*' sort false
zstyle ':completion:*:descriptions' format '[%d]'
zstyle ':completion:*' rehash true
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}' 'r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' menu no
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'eza -1 --color=always $realpath'
zstyle ':fzf-tab:complete:zz:*' fzf-preview 'eza -1 --color=always $realpath'
zstyle ':fzf-tab:*' switch-group '<' '>'

# export PYENV_ROOT="$HOME/.pyenv"
# [[ -d "$PYENV_ROOT/bin" ]] && path=("$PYENV_ROOT/bin" $path)
# eval "$(pyenv init -)"
# eval "$(pyenv virtualenv-init -)"

export PNPM_HOME="/Users/blumsicle/Library/pnpm"
case ":$PATH:" in
    *":$PNPM_HOME:"*) ;;
    *) export PATH="$PNPM_HOME:$PATH" ;;
esac

export EDITOR=nvim
export HOMEBREW_NO_ENV_HINTS=1

alias -g ...='../..'
alias -g ....='../../..'
alias -g .....='../../../..'
alias -g ......='../../../../..'

alias cat='bat'
alias man='batman'
alias q='exit'
alias nvims='nvim --listen ~/.cache/nvim/server.pipe'
alias nvimc='nvim --server ~/.cache/nvim/server.pipe --remote-silent'
alias bupp='brew update && brew upgrade --fetch-HEAD --greedy && brew autoremove && brew cleanup'

alias nvimf='fzf --preview "bat --color=always {}" --preview-window "~3" --multi --bind "enter:become(nvim {+})"'
alias nvimg='git grep --line-number . | fzf --delimiter : --nth 3.. --bind "enter:become(nvim {1} +{2})"'

function gosrc() {
    local srcdir="$HOME/go/src/github.com/blumsicle"
    cd "$srcdir"

    [[ $1 == "." ]] && return

    local query=$([[ $1 != "" ]] && echo "--query $1")
    cd "$(fd --exact-depth=1 | fzf --select-1 ${=query} || echo .)"
}
